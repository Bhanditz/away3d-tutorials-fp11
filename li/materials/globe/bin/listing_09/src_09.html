<!DOCTYPE HTML>
<html>
	<head>
	<title>Listing 1</title>
	<!-- AS3 Syntax Highlighter -->
	<script type="text/javascript" src="http://away3d.com/static/syntaxHighlighter/scripts/shCore.js"></script>
	<script type="text/javascript" src="http://away3d.com/static/syntaxHighlighter/scripts/shBrushAS3.js"></script>
	<link type="text/css" rel="stylesheet" href="http://away3d.com/static/syntaxHighlighter/styles/shCoreEclipse.css"/>
	<script type="text/javascript">SyntaxHighlighter.all();</script>
</head>

<body style="background: white; font-family: Helvetica">

<pre class="brush: as3;">
package li.materials.globe.src
{

	import away3d.containers.ObjectContainer3D;
	import away3d.entities.Mesh;
	import away3d.materials.TextureMaterial;
	import away3d.primitives.SphereGeometry;
	import away3d.utils.Cast;

	import li.base.ListingBase;

	public class GlobeListing01 extends ListingBase
	{
		// Diffuse map for the Earth's surface.
		[Embed(source="../../../../embeds/solar/earth_surface_2048.jpg")]
		public static var EarthSurfaceDiffuse:Class;

		// Diffuse map for the Moon's surface.
		[Embed(source="../../../../embeds/solar/moon_1024.jpg")]
		public static var MoonSurfaceDiffuse:Class;

		private var _earth:ObjectContainer3D;
		private var _moon:ObjectContainer3D;

		public function GlobeListing01() {
			super();
		}

		override protected function onSetup():void {
			createEarth();
			createMoon();
		}

		private function createEarth():void {
			// Material.
			var earthMaterial:TextureMaterial = new TextureMaterial( Cast.bitmapTexture( EarthSurfaceDiffuse ) );
			// Container.
			_earth = new ObjectContainer3D();
			_earth.rotationY = rand( 0, 360 );
			_view.scene.addChild( _earth );
			// Surface geometry.
			var earthSurface:Mesh = new Mesh( new SphereGeometry( 100, 200, 100 ), earthMaterial );
			_earth.addChild( earthSurface );
		}

		private function createMoon():void {
			// Material.
			var moonMaterial:TextureMaterial = new TextureMaterial( Cast.bitmapTexture( MoonSurfaceDiffuse ) );
			// Container.
			_moon = new ObjectContainer3D();
			_moon.rotationY = rand( 0, 360 );
			_view.scene.addChild( _moon );
			// Surface geometry.
			var moonSurface:Mesh = new Mesh( new SphereGeometry( 50, 200, 100 ), moonMaterial );
			moonSurface.x = 1000;
			_moon.addChild( moonSurface );
		}

		override protected function onUpdate():void {
			super.onUpdate();
			_earth.rotationY += 0.05;
			_moon.rotationY -= 0.005;
		}

		private function rand( min:Number, max:Number ):Number {
			return (max - min)*Math.random() + min;
		}
	}
}
</pre>
</body>
</html>