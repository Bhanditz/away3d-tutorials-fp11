<div class="tutorialMetadata">[[Category:Starling]]</div>
<article>
<aside>
<span class="requirement">User Level</span>
Intermediate

<span class="requirement">Prerequisite knowledge:</span>
Good ActionScript 3 programming experience with Flash Builder or similar integrated development environment (IDE) is recommended along with experience of developing Stage3D based applications using both Away3D and Starling.

<span class="requirement">Required tools</span>
Flash Builder or similar IDE.

<span class="requirement">Sources</span>
<a href="https://github.com/away3d/away3d-tutorials-fp11/tree/master/greg/starling/interoperation" target="_blank">Complete sources</a>

<span class="requirement">Downloads</span>
--- add downloadable zip ---
</aside>
This tutorial details how to use Away3D 4.x to combine 3D (Away3D) and 2D (Starling) content, allowing them to share the same Stage3D instance.

<h4>Contents</h4>
<nav><ul>
<li><a href="#intro">Introduction</a></li>
<li><a href="#init">Initializing the shared Stage3D instance</a></li>
<li><a href="#construct">Constructing the render layers</a></li>
<li><a href="#render">Rendering the layers</a></li>
<li><a href="#manual">Taking control – Manually</a></li>
<li><a href="#auto">Taking control – Automatically</a></li>
<li><a href="#conclusion">Where to go from here</a></li>
</ul></nav>
<section id="intro"><h4>Introduction</h4>
The Away3D 4.0 Gold release includes an API called <code>Stage3DManager</code> for integrating the rendering process of Actionscript libraries that use Stage3D. In this tutorial, we see how this API can be used to combine Away3D and Starling frameworks into a single, high-performance rendering loop. 

Since the introduction of Stage3D in Air and the Flash Player, numerous actionscript frameworks have been introduced and upgraded to take full advantage of the new found GPU acceleration. Away3D is one of those frameworks which now leverages the new GPU features allowing developers to create incredibly rich 3D interactive experiences. Starling, similarly, is another framework exploiting the power of Stage3D but is focused on providing GPU accelerated 2D graphics.

There has been several requests to combine both 2D and 3D frameworks together in a single application, however, to do this has previously required modification to the APIs themselves or having each framework render within it's own separate Stage3D instance. Unfortunately, along with a greater performance overhead, there currently isn't support for transparent Stage3D instances so overlaying them obscures the instance beneath.

<figure>
<img src="http://away3d.com/images/uploads/integration.gif"> 
<figcaption>Figure 1. Stage, Stage3D instances and StageVideo layering.</figcaption>
</figure>     

With the 'production ready' release of Away3D 4.0, comes the ability to easily combine multiple frameworks into a single Stage3D instance. You can now layer any combination and any number of Away3D or Starling instances. In fact, there is no reason any Stage3D framework could be used - See the technical details at the bottom of this page.

This tutorial will take you through an example of having a background Starling layer, a middle Away3D layer and a foreground Starling layer.

<figure>
<a href="http://away3d.com/wikicontent/tutorials/greg/starling/interoperation/bin/Intermediate_One_Away3D_Two_Starling_Layers.html" rel="shadowbox; width=800;height=600;"><img class="interactiveFigure" src="http://away3d.com/images/uploads/integrationdemo.jpg"></a>  
<figcaption>
Listing 1. Away3D & Starling Interoperation example.
<a href="https://github.com/away3d/away3d-tutorials-fp11/blob/master/greg/starling/interoperation/src/Intermediate_One_Away3D_Two_Starling_Layers.as" target="_blank">Source code.</a>
</figcaption>
</figure>
</section>
<section id="init"><h4>Initializing the shared Stage3D instance</h4>
Under normal operation, when only Away3D is being used, management of the Stage3D is handled internally. It is this internal management that make it extremely easy to work with, but also causes difficulties in sharing the Stage3D instance. What is necessary is access to a common Stage3D instance where all the rendering will happen, for each framework. This is done through the <code>Stage3DManager</code> and <code>Stage3DProxy</code> classes.  

<pre class="brush: as3">
// Stage manager and proxy instances
private var stage3DManager : Stage3DManager;
private var stage3DProxy : Stage3DProxy;
 
/**
 * Global initialise function
 */
private function init():void
{
    stage.scaleMode = StageScaleMode.NO_SCALE;
    stage.align = StageAlign.TOP_LEFT;
 
    initProxies();
}
 
/**
 * Initialise the Stage3D proxies
 */
private function initProxies():void
{
    // Define a new Stage3DManager for the Stage3D objects
    stage3DManager = Stage3DManager.getInstance(stage);
 
    // Create a new Stage3D proxy to contain the separate views
    stage3DProxy = stage3DManager.getFreeStage3DProxy();
    stage3DProxy.addEventListener(Stage3DEvent.CONTEXT3D_CREATED, onContextCreated);
    stage3DProxy.antiAlias = 8;
    stage3DProxy.color = 0x0;
}
</pre>

The <code>Stage3DManager</code> works as a singleton so to access it we have to explicitly request the singleton instance using the <code>getInstance()</code> method. Using the <code>Stage3DManager</code> instance, we can then make a request to allocate the next available Stage3D for our rendering. This isn't an immediate process so we need to wait until one is allocated for our purposes. This is done by listening for the <code>Stage3DEvent.CONTEXT3D_CREATED</code> event.

While we are working with the <code>Stage3DProxy</code>, this is as good a time as any to set some of the properties such as the overral antialias level and background colour.
</section>
<section id="construct"><h4>Constructing the render layers</h4>
When the <code>Stage3DEvent.CONTEXT3D_CREATED</code> event is fired, we are ready to carry on and construct our layers for rendering.

As mentioned, in this example we'll have one Away3D scene and two Starling scenes. Rather than focusing on the contents of the scenes, I'll detail what's required for setting each one up in preparation for layered rendering.
   
<pre class="brush: as3">
private function onContextCreated(event : Stage3DEvent) : void {
    initAway3D();
    initStarling();
    initMaterials();
    initObjects();
    initButton();
    initListeners();
    initListeners();
}
 
/**
 * Initialise the Away3D views
 */
private function initAway3D() : void
{
    // Create the first Away3D view which holds the cube objects.
    away3dView = new View3D();
    away3dView.stage3DProxy = stage3DProxy;
    away3dView.shareContext = true;
 
    hoverController = new HoverController(away3dView.camera, null, 45, 30, 1200, 5, 89.999);
 
    addChild(away3dView);
 
    addChild(new AwayStats(away3dView));
}
 
/**
 * Initialise the Starling sprites
 */
private function initStarling() : void
{
    // Create the Starling scene to add the checkerboard-background
    starlingCheckerboard = new Starling(StarlingCheckerboardSprite, stage, stage3DProxy.viewPort, stage3DProxy.stage3D);

    // Create the Starling scene to add the particle effect
    starlingStars = new Starling(StarlingStarsSprite, stage, stage3DProxy.viewPort, stage3DProxy.stage3D);
}
</pre>

The <code>onContextCreated</code> method goes through the typical setup to define the 3D scene, objects, materials, event listeners and also the Starling setup.

In the <code>initAway3D</code> method, we construct the View3D object as usual for the Away3D scene, however, we set the <code>.stage3DProxy</code> property to our shared Stage3DProxy instance and we also instruct the View3D to work with a shared Stage3D by setting <code>.shareContext=true</code>.  Similarly the <code>initStarling</code> method creates the Starling scenes. The requirement of each scene is to pass a reference to the Starling sprite scene, the Stage and in the case of a shared Stage3D, we also pass a Rectangle defining the size of the view port and also a reference to our Stage3D instance. Fortunately, both of these are accessible directly from the Stage3DProxy instance as <code>.viewPort</code> and <code>.stage3D</code> properties. 

NOTE: With Starling, if you wish to move or resize the viewport of a Sprite, it is necessary to manage the viewport Rectangle externally to Starling, as referencing the <code>Starling.viewPort</code> property returns a clone rather than the original object reference. Setting it externally, passing it on the constructor and changing the properties of the external rectangle updates Starling rendering correctly.

Each Away3D scene and Starling sprite layer now has a reference to the shared Stage3D instance. The rest of the scene setup continues as normal. As you can see in the complete demo above, it allows you to cycle through, swapping the layers around. Initially, at the back (the first rendered layer) is a Starling layer that rotates a checkerboard pattern. On top of this is the Away3D scene that has 5 cubes arranged in a cross formation, on a wireframe grid. Finally, the top layer, which is rendered last, is the Starling particle effect.
</section>
<section id="render"><h4>Rendering the layers</h4>
Within Stage3D frameworks, there are certain functions in the rendering process which need disabling when the Stage3D is being shared. In an <strong>extremely simplified</strong> view, for the purposes of this tutorial, the rendering of a scene requires the scene to be cleared - <code>clear()</code>, the elements rendered - <code>drawTriangles()</code> and finally scene displayed - <code>present()</code>. It is this 'clearing' and 'displaying' of each framework that stops Stage3Ds being shared, by default.

<figure>
<img src="http://away3d.com/images/uploads/integrationrender1.gif"/>
<figcaption>Figure 2. Simplified Stage3D rendering process.</figcaption>
</figure>

Now that Away3D and Starling have been instructed to use a shared Stage3D instance, they only need to consider the rendering part of the process, leaving the clearing and presenting responsibility of the <code>Stage3DProxy</code> object.

There are two rendering options available through the <code>Stage3DProxy</code> class which are very similar but provide different control of the clearing and presenting. The manual approach requires you to explicitly call the stage3DProxy's <code>.clear()</code> and <code>.present()</code> methods around your layer rendering calls whereas the more automatic approach handles that for you. Both are described here.

<figure>
<img src="http://away3d.com/images/uploads/integrationrender2.gif"/>
<figcaption>Figure 3. Shared Stage3D layered rendering process</figcaption>
</figure>
</section>
<section id="manual"><h4>Taking control - the manual approach</h4>
   
<pre class="brush: as3">
/**
 * Set up the rendering processing event listeners
 */
private function initListeners() : void {
	stage.addEventListener(Event.ENTER_FRAME, onEnterFrame);
}

/**
 * The main rendering loop
 */
private function onEnterFrame(event : Event) : void {

	// Clear the Context3D object
	stage3DProxy.clear();

	// Render the Starling animation layer
	starlingCheckerboard.nextFrame();

	// Render the Away3D layer
	away3dView.render();

	// Render the Starling stars layer
	starlingStars.nextFrame();

	// Present the Context3D object to Stage3D
	stage3DProxy.present();
}
</pre>

In this piece of code, we listen for an <code>Event.ENTER_FRAME</code> event on the main stage instance, then within the listener method, we call the <code>stage3DProxy.clear();</code> method then render each layer in turn, followed by the <code>stage3DProxy.present();</code>. This gives you complete control of how the layers are rendered and when/if the <code>clear()</code> and <code>present()</code> calls need to be made. If nothing is changing then the display need not be re-rendered.
</section>
<section id="auto"><h4>The easy life - the automatic approach</h4>
   
<pre class="brush: as3">
/**
 * Set up the rendering processing event listeners
 */
private function initListeners() : void {
	stage3DProxy.addEventListener(Event.ENTER_FRAME, onEnterFrame);
}

/**
 * The main rendering loop
 */
private function onEnterFrame(event : Event) : void {

	// Render the Starling animation layer
	starlingCheckerboard.nextFrame();

	// Render the Away3D layer
	away3dView.render();

	// Render the Starling stars layer
	starlingStars.nextFrame();
}
</pre>

This approach is subtly different in that the <code>Event.ENTER_FRAME</code> event listener is added to the <code>Stage3DProxy</code> object, rather than the stage. As such, it internally manages when the <code>clear()</code> and <code>present()</code> calls are performed, leaving you to decide on how the rendering is done. In the above code, you can see that only the layer render calls are performed in the event listener method.
</section>
<section id="conclusion"><h4>Conclusion</h4>
Through this tutorial, you have been introduced to the <code>Stage3DManager</code> and more importantly the <code>Stage3DProxy</code> and how they can be used in conjuntion with Away3D and Starling frameworks to benefit from GPU accerlerated 3D and 2D graphics together, which is increasingly becoming important to for rich interactive applications and games.

Greg Caldwell - The Away3D Team
</section>
</article>